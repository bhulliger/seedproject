// ############################################################################
// Source Sets & BuildScript
// ############################################################################
buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.hidetake:gradle-ssh-plugin:0.3.10'
	}
}

// ############################################################################
// Artifactory Tasks
// ############################################################################
apply plugin: org.hidetake.gradle.ssh.plugin.SshPlugin

ext.downloadedArtifact = file("$buildDir/download/$project.name-${version}-${config.environment}.zip") 
ext.autoinstallRemoteDirectory = "/var/opt/testing/$config.projectName/"

task downloadArtifactArchive(type: helper.BinaryDownload) {
	description 'Download an artifact from the artifactRepository (configured in build.groovy)'

	ext {
		repoPath = project.group.replaceAll("\\.", "/")
		repoBaseArtifactName = "$rootProject.name-$project.name"
		repoVersion = project.version.toString()
		repoArtifact = "$rootProject.name-$project.name-${version}-${config.environment}.zip"
		binaryUrl = "${config.artifactRepository.url}/artifactory/simple/${config.binaryRepository.name}/${repoPath}/${repoBaseArtifactName}/${repoVersion}/${repoArtifact}"
	}

	sourceUrl = binaryUrl
	targetBinary = downloadedArtifact
}

remotes {
	remoteServer {
		host = config.server.ip
		user = config.server.username
		password = config.server.password
	}
}

task install(type: SshTask, dependsOn: downloadArtifactArchive) {
	description 'installs the artifact on the server (by SSH)'

	ssh {
		knownHosts = allowAnyHosts
		pty = true
	}

	session(remotes.remoteServer) {
		execute("set + e; rm -R $autoinstallRemoteDirectory*")
		put downloadedArtifact, "$autoinstallRemoteDirectory"
		execute("cd $autoinstallRemoteDirectory; unzip -o distribution-*.zip; ./autoinstall.sh;")
	}
}