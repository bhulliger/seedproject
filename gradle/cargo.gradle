// ############################################################################
// Source Sets & BuildScript
// ############################################################################
buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'org.gradle.api.plugins:gradle-cargo-plugin:1.5'
	}
}

apply plugin: org.gradle.api.plugins.cargo.CargoPlugin

// ############################################################################
// Dependencies
// ############################################################################
dependencies {

	cargo 		group: 'org.codehaus.cargo', 		name: 'cargo-core-uberjar', 			version: '1.4.8'
	cargo 		group: 'org.codehaus.cargo', 		name: 'cargo-ant', 						version: '1.4.8'
	cargo 		group: 'org.jboss.as',				name: 'jboss-as-controller-client', 	version: '7.2.0.Final'
	cargo 		group: 'org.wildfly', 				name: 'wildfly-controller-client', 		version: '8.0.0.Final'
	cargo 		group: 'org.codehaus.cargo', 		name: 'cargo-ant', 						version: '1.4.5'    
}


// ############################################################################
// Cargo Configuration
// ############################################################################
cargo {
	containerId = config.container.containerId?.trim() ?: 'tomcat8x'
	port = config.container.port ?: 8080
	
	deployable {
		context = config.container.context?.trim() ?: "${rootProject.name}-${project.name}"
		file = file("$buildDir/libs/${project.name}-${version}.war")
	}

	// Remote Deployment Configuration ########################################
	remote {
		containerId = config.container.containerId
		hostname = config.container.remote.hostname
		username = config.container.remote.username
		password = config.container.remote.password
	}

	local {
		installer {
			installUrl = config.container.installUrl?.trim() ?: 'http://mirror.switch.ch/mirror/apache/dist/tomcat/tomcat-8/v8.0.9/bin/apache-tomcat-8.0.9.zip'
			downloadDir = file("$buildDir/download")
			extractDir = file("$buildDir/extract")
		}
	}
}

task cargoAddJBossModules(type: Copy) {
	description 'Copies the JBoss Module to Cargo'
	// Modules
	from "$rootDir/dist/src/main/resources/jboss/modules"
	into "$buildDir/extract/jboss-as-7.1.1.Final/jboss-as-7.1.1.Final/modules"
}



cargoRunLocal.dependsOn assemble
cargoRunLocal.dependsOn cargoAddJBossModules